#!/bin/bash

inputFile=$1
index=$2
name=$3
displayName=$4
start=$5
stepSize=$6

############################################################
#                                                          #
#                     Get some gnuplot on                  #
#                                                          #
############################################################

############################################################
#                          Trace                           #
############################################################

gnuplot << END

set terminal postscript enhanced eps color font 'Times-Roman, 25' size 10, 5
set output "${name}_trace.eps"

set datafile separator ","

set rmargin 10

set title "Trace"

set xtics nomirror
set xlabel 'Iteration'

set ytics nomirror
set ylabel "$displayName"

stats "$inputFile" using $index every ::$start nooutput
varMean=STATS_mean

stats "$inputFile" using 2 every ::$start nooutput
depthMean=STATS_mean

delta = 0.5 * $stepSize * depthMean

set label "Mean" at graph 1.01, first varMean left
set label "Mean + {/Symbol D}" at graph 1.01, first varMean+delta left
set label "Mean - {/Symbol D}" at graph 1.01, first varMean-delta left

plot \
varMean-delta with filledcurves y1=varMean+delta lt 1 lc rgb '#bbbbdd' notitle, \
varMean with lines lt 3 lw 2 notitle, \
"$inputFile" using 0:$index every ::$start with lines ls 1 lw 2 linecolor rgb 'red' notitle

END

############################################################
#                     logProb Correlation                  #
############################################################

gnuplot << END

set terminal postscript enhanced eps color font 'Times-Roman, 25' size 5, 5
set output "${name}_logp.eps"

set datafile separator ","

set title "logPosterior Correlation"

set xtics nomirror
set xlabel "$displayName"

set ytics nomirror
set ylabel 'logPosterior'

plot \
"$inputFile" using $index:1 every ::$start with points ls 1 ps 2 linecolor rgb 'blue' notitle

END

############################################################
#                   Tree Depth Correlation                 #
############################################################

gnuplot << END

set terminal postscript enhanced eps color font 'Times-Roman, 25' size 5, 5
set output "${name}_treedepth.eps"

set datafile separator ","

set title "Tree Depth Correlation"

set xtics nomirror
set xlabel "$displayName"

stats "$inputFile" using 2 every ::$start nooutput

set ytics nomirror
set ylabel 'Tree Depth'
set yrange[STATS_min-1:STATS_max+1]

plot \
"$inputFile" using $index:2 every ::$start with points ls 1 ps 2 linecolor rgb 'blue' notitle

END